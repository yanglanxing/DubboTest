plugins {
    id('java')
    id('org.jetbrains.intellij') version '1.10.0'
}

group = group
version = version

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

repositories {
    maven { url 'https://maven.aliyun.com/repository/public/' }
    mavenLocal()
    mavenCentral()
}

intellij {
    pluginName = name
    version = ideaVersion
    plugins = ['com.intellij.java']
}

dependencies {
    implementation('org.apache.dubbo:dubbo:3.0.0') {
        exclude group: 'org.springframework'
    }
    implementation('com.google.code.gson:gson:2.8.9')
    implementation('org.javassist:javassist:3.20.0-GA')
    implementation('org.apache.commons:commons-lang3:3.11')
    implementation('org.apache.dubbo:dubbo-dependencies-zookeeper:3.0.0') {
        exclude group: 'org.slf4j'
        exclude group: 'org.apache.zookeeper'
    }
    implementation fileTree(dir: 'lib', includes: ['*.jar'])
}

tasks {
    runIde {
        jvmArgs '--add-exports', 'java.base/jdk.internal.vm=ALL-UNNAMED'
        autoReloadPlugins = true
    }

    patchPluginXml {
        pluginDescription = file('includes/pluginDescription.html').text
        changeNotes = file('includes/pluginChanges.html').text
    }

    publishPlugin {
        token = System.getenv("publishToken")
        channels = System.getenv("publishChannels")
    }
}
